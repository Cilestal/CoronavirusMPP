apply plugin: 'kotlin-multiplatform'

def getClosureByNameOrEmpty(String name) {
    if (this.properties.containsKey(name)) {
        return this.properties.find { it.key == name }.value
    } else {
        return {}
    }
}

kotlin {
    targets {
        jvm()

        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos")     \
                                  ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'iOS')
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
                implementation kotlinx_serialization
            }
            dependencies(getClosureByNameOrEmpty("commonDependencyGroup"))
        }

        iOSMain {
            dependsOn commonMain
            dependencies(getClosureByNameOrEmpty("iOSDependencyGroup"))

            dependencies {
                implementation kotlinx_serialization_native
            }
        }

        jvmMain {
            dependsOn commonMain
            dependencies(getClosureByNameOrEmpty("jvmDependencyGroup"))

            dependencies {
                implementation kotlinx_serialization_jvm
            }
        }

        jvmTest {
            dependsOn jvmMain
            dependencies(getClosureByNameOrEmpty("jvmTestDependencyGroup"))

            project.jvmTest {
                useJUnitPlatform()
            }

            dependencies {
                api project(":common-test")
                api kotlin_reflect
            }
        }
    }
}

afterEvaluate {
    if (!project.hasProperty('test')) {
        task test()
    }
    tasks.test.dependsOn jvmTest
}